[    
    Copyright 2021 TomÃ¡s Vallotton

   Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

        http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.
]

    ; First we write the boilerplate code including the stdio and stdlib headers 
    ; and initializing the memory with calloc
++++++++++.-[->++++<]>-.[->+++<]>.+++++.-----------.+++++++++.+++++++++.+[->+++<]>++.+.--[--->+<]>-.--[->
++<]>.--[->++<]>-.+.++[->+++<]>++.+++++.++++++.[->+++++<]>+++.+[--->+<]>+++.++[->+++<]>.>++++++++++.-[->+
+++<]>-.[->+++<]>.+++++.-----------.+++++++++.+++++++++.+[->+++<]>++.+.--[--->+<]>-.--[->++<]>.--[->++<]>
-.+.++[->+++<]>++.++++++++.---.-------.[-->+<]>---.+[--->+<]>+++.++[->+++<]>.>++++++++++.--[----->+<]>+.+
++++.++++++.[---->+<]>+++.+[----->+<]>.------------.++++++++.+++++.++[++>---<]>.-----[->+++<]>.+++++.++++
++.[---->+<]>+++.[->+++<]>+.--[--->+<]>---.-----------.----.+[->+++<]>.------------.+[->+++<]>.+++++.----
---.--[--->+<]>---.[-->+++++<]>+++.++++++++++.-[--->+<]>--.--[--->+<]>---.-----------.--[--->+<]>-.--[->+
++<]>-.++.-[->++++++<]>+.---------.-[->++++<]>-.>++++++++++.[->+++<]>++....+[->+++<]>.+++++.-------.--[--
->+<]>---.[-->+++++<]>+++.++++++++++.--[--->++<]>.++++.--.[-->+++++<]>+++.-[->++<]>-.+[-->+<]>+.+[->+++<]
>.--.+++++++++++..+++.------------.[->+++<]>-.++++++++++++++.-..--.+++.----------.------------.[-->+++<]>
+.--------.-[-->+++<]>-.>++++++++++.[->+++<]>++....+++[->++<]>.+++.+++.-------.-[-->+<]>--.++++++++++.[--
>+++++<]>.+++++.++.+++++.-.[-->+<]>+.>++++++++++.[->+++<]>++....+++[->++<]>.+++.+++.-------.-[-->+<]>--.+
+++++++++.+[----->+<]>.++++++.-.----.+++++.-.[-->+<]>+.>++++++++++.[->+++<]>++....-[--->++<]>-.---.[--->+
<]>--.++++++++.+++++++++.-[--->++<]>.+.--[->++<]>-.+[-->+<]>+.[->+++<]>+.--[--->+<]>---.-----------.----.
[->+++<]>.---------.-[->++++<]>-.>++++++++++.[->+++<]>++........-[--->++<]>-.+++++.++.+++++.-.[---->+<]>+
++.-[->++<]>-.+[-->+<]>+.++[->+++<]>.+++++++++.+.-----------.+++++++++.++[++>---<]>.--[--->+<]>-.--[--->+
<]>---.-----------.--[--->+<]>-.--[->+++<]>-.+[-->+<]>+++.--[->++<]>-.-[-->+<]>--.------------.++.++++[->
+++<]>.[--->+<]>----.+++++++.-[-->+++<]>-.>++++++++++.[->+++<]>++....-[->++++<]>+.-[---->+<]>+.+[->+++<]>
++.+++++++.+++++++.++++[->+++<]>.--[--->+<]>-.-[->++++<]>-.>++++++++++.[->+++<]>++........-[--->++<]>-.++
+++.++.+++++.-.[---->+<]>+++.-[->++<]>-.+[-->+<]>+.---[->++++<]>-.+.++[->+++<]>++.+++++.+++++.[-->+<]>+++
+.>++++++++++.[->+++<]>++....-[->++++<]>+.>++++++++++.[->+++<]>++....-[--->++<]>-.---.[--->+<]>--.+++++++
+.+++++++++++.[--->++<]>--.--[->++<]>.+.+[-->+<]>+.[->+++<]>+.--[--->+<]>---.-----------.----.[->+++<]>.-
--------.-[->++++<]>-.>++++++++++.[->+++<]>++........+++++[->+++<]>.++++++.-.----.+++++.-.[---->+<]>+++.-
[->++<]>-.+[-->+<]>+.++[->+++<]>.+++++++++.+.-----------.+++++++++.++[++>---<]>.--[--->+<]>-.--[--->+<]>-
--.-----------.--[--->+<]>-.--[->+++<]>-.+[-->+<]>++++.---[->++<]>-.-[-->+<]>--.------------.++.[-->+++++
++<]>.[------->++<]>.+++++++.-[-->+++<]>-.>++++++++++.[->+++<]>++....-[->++++<]>+.-[---->+<]>+.+[->+++<]>
++.+++++++.+++++++.++++[->+++<]>.--[--->+<]>-.-[->++++<]>-.>++++++++++.[->+++<]>++........+++++[->+++<]>.
++++++.-.----.+++++.-.[---->+<]>+++.-[->++<]>-.+[-->+<]>+.---[->++++<]>-.+.++[->+++<]>++.+++++++++++.++++
++.-.[-->+<]>+.>++++++++++.[->+++<]>++....-[->++++<]>+.

// Note here X is used to denote some special 
// character in brainfuck's syntax while G is used
// for some garbage value that is unknown
// A questionmark is used to denote values that
// have meaning when they are null

// We begin to parse in a loop
// until a EOF character is fed (minus 1)
,+
[
                                              *
    // copy input char 9 times 0|c|c|c|c|c|c|0|0|0|0|0|0
    -[>+>+>+>+>+>+>+>+>+>+<<<<<<<<<<-]
                                              *
    // move to the outermost copy 0|c|c|c|c|c|c|0|0|0|0|0|0  
    >>>>>>>>>>           
    
                                       *
    // write a plus sign:  0|c|c|c|c|c|c|X|0|0|0|0|0 
    >>++[------<+>][-]<<
                                          *
    // compare characters:  0|c|c|c|c|c|0|?|0|0|0|0|0  
    >[-<->]<[>-<[-]]>+
    
                                                         *
    // if they are equal write increment ptr 0|c|c|c|c|c|0|0|0|0|0|0|0 
    [
      [-]++[------>+<]>..-.--[--->++<]>.++++.--.[-->+<]>++.>++++++++++.
      [-]<[-]<[-]<[-]<
    ]<
                                      *  
    // write a minus sign:  0|c|c|c|c|c|X|0|0|0|0|0 
    >++[------<+>][-]<++<
    
                            *
    // compare  0|c|c|c|c|0|?|0|0|0|0|0 
    >[-<->]<[>-<[-]]>+
                                                        *
    // if they are equal write decrement ptr  0|c|c|c|c|0|0|0|0|0|0|0 
    [
      [-]++[------>+<]>++..---.--[--->++<]>.++++.--.[-->+<]>++.>++++++++++.
      [-]<[-]<[-]<[-]<
    ]<
                                     *
    // write forward move  0|c|c|c|c|X|0|0|0|0|0|0 
    >----[----<+>][-]<-<
                           *
    // compare 0|c|c|c|c|0|?|0|0|0|0|0 
    >[-<->]<[>-<[-]]>+
    
                                              *
    // if they are equal move forward 0|c|c|c|0|0|0|0|0|0|0|0 
    [
        [-]++[------>+<]>..[----->+<]>+.++++.--.[-->+<]>++.>++++++++++.
        [-]<[-]<[-]<[-]<
    ]<

    // write backwards move
    >----[----<+>][-]<---<
                               
    // compare            0|c|c|0|0|0|0|0|0|0|0|0 
    >[-<->]<[>-<[-]]>+
    [
       [-]++[------>+<]>++..--[----->+<]>+.++++.--.[-->+<]>++.>++++++++++.
       [-]<[-]<[-]<[-]<
    ]<

    // write a comma
    >++[------<+>][-]<+<
    
    // compare 
    >[-<->]<[>-<[-]]>+
    [
       [-]++[------>+<]>-.--[--->++<]>.++++.--.[-->+++++<]>+++.-[->++<]>-.+[-->+<]>+.++[->+++<]>.+.--.[---
       >+<]>---.++[->+++<]>+.[->+++<]>-.-----[->+++<]>.+++++.++.+++++.-.+[--->+<]>++.-[-->+++<]>-.
       [-]<[-]<[-]<[-]<[-]<[-]<[-]<[-]<[-]<[-]<[-]<[-]<
    ]<

    // write period
    >++[------<+>][-]<+++<

    // compare
    >[-<->]<[>-<[-]]>+ 
    [
        [-]--[----->+<]>.++++++++++.+++++.-.++[->+++<]>+.[->+++<]>-.++.--[--->++<]>.++++.--.[++>---<]>+.---
        ---------.+++++[->+++<]>.++++++.-.----.+++++.-.+[--->+<]>++.-[-->+++<]>-.>++++++++++.
        [-]<[-]<[-]<[-]<[-]<[-]<[-]<[-]<[-]<
    ]<

    print "while (*ptr) {"
    >+[---<++>][-]<+++++<
    >[-<->]<[>-<[-]]>+
    [
        [-]------[-->+++<]>.+[->+++<]>.+.+++.-------.--[--->+<]>-.++++++++.++.--[--->++<]>.++++.--.[--->+<]>+
        ++.---------.-[->++++<]>-.
        [-]<[-]<[-]<[-]<[-]<[-]<
    ]<
    

    print "}"
    >+[---<++>][-]<+++++++<
    >[-<->]<[>-<[-]]>+
    [
        [-]--[-->+++<]>.
        [-]<
    ][-]<<
    
    // EOF = minus 1; so this will exit in that case
    ,+
]


// close main function
++++++++++.[------->+++<]>.-------------.[--->+<]>---.+.---.----.-[->+++++<]>
-.[-->+++<]>.+++++++++++.+[-->+<]>++.>++++++++++.>--[-->+++<]>. 
